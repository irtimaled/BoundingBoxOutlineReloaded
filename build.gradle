buildscript {
    repositories {
        jcenter()
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
    }
    dependencies {
        classpath "net.fabricmc:fabric-loom:0.8-SNAPSHOT"
    }
}

apply plugin: 'fabric-loom'
apply plugin: 'java'
apply plugin: 'idea'

group 'com.irtimaled'
version project.buildVersion + '-' + project.mcVersion
archivesBaseName = 'BBOutlineReloaded'

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

tasks.withType(JavaCompile).configureEach {
    // ensure that the encoding is set to UTF-8, no matter what the system default is
    // this fixes some edge cases with special characters not displaying correctly
    // see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
    // If Javadoc is generated, this must be specified in that task too.
    it.options.encoding = "UTF-8"

    // Minecraft 1.17 (21w19a) upwards uses Java 16.
    it.options.release = 16
}

dependencies {
    minecraft 'com.mojang:minecraft:' + project.mcVersion
    mappings 'net.fabricmc:yarn:' + project.mcVersion + '+build.29:v2'
    modImplementation 'net.fabricmc:fabric-loader:0.11.6'
}

loom {
    accessWidener "src/main/resources/bbor.accesswidener"
}

minecraft {
    refmapName = 'mixins.bbor.refmap.json'
}

processResources {
    filesMatching('fabric.mod.json') {
        expand version: project.version
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Main-Class": "com.irtimaled.bbor.Main",
                "Specification-Title": "bbor",
                "Specification-Vendor": "Irtimaled",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": "${version}",
                "Implementation-Vendor": "Irtimaled",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

afterEvaluate {
    remapJar.classifier = 'fabric'
}
